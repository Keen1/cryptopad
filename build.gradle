plugins {
    id 'application'
    id 'org.beryx.jlink' version '3.1.1'

}

group = 'app.keendev'
version = '1.0-RELEASE'

repositories {
    mavenCentral()
}

application {
    mainClass = 'drivers.GuiDriver'
    mainModule = 'cryptopad'

    applicationDefaultJvmArgs = ['-Dawt.appName=cryptopad']
}

jlink {

    options = ['--ignore-signing-information', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--bind-services']

    mergedModule{
        additive = true
    }

    launcher {
        name = 'cryptopad'

    }


    jpackage{

        installerType ='deb'
        imageName = 'cryptopad'
        appVersion = version
        vendor = 'Keendev'
        icon = file('src/main/resources/icon.png')

        installerOptions = ['--linux-menu-group','Utilities',
                            '--linux-app-category', 'Utility',
                            '--linux-shortcut']


    }

    forceMerge('com.formdev.flatlaf', 'com.formdev.flatlaf.intellijthemes', 'org.bouncycastle.provider')
}





dependencies {
    //testing dependencies
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation group:'org.mockito', name: 'mockito-core', version: '5.15.2'
    testImplementation group:'org.mockito', name: 'mockito-junit-jupiter', version: '5.15.2'

    //application dependencies
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.80'
    implementation 'com.formdev:flatlaf:3.4'
    implementation 'com.formdev:flatlaf-intellij-themes:3.5.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0-rc2'


}

jar {
    manifest {
        attributes 'Main-Class': 'drivers.GuiDriver'
    }



    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/versions/**'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'


}

test {
    useJUnitPlatform()
}