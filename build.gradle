plugins {
    id 'application'
    id 'org.beryx.jlink' version '3.1.1'

}

group = 'app.keendev'
version = '1.0'

repositories {
    mavenCentral()
}

application {
    mainClass = 'drivers.GuiDriver'
    mainModule = 'cryptopad.main'

    applicationDefaultJvmArgs = ['--add-opens', 'java.desktop/sun.awt.X11=cryptopad.main']
}

jlink {

    options = ['--ignore-signing-information', '--strip-debug', '--no-header-files', '--no-man-pages', '--bind-services']

    mergedModule{
        additive = true
    }

    launcher {
        name = 'cryptopad'
        jvmArgs = ['--add-opens', 'java.desktop/sun.awt.X11=cryptopad.main']


    }


    jpackage{

        installerType ='deb'
        imageName = 'cryptopad'
        installerName = 'cryptopad'
        vendor = 'Zach Keener'
        appVersion = version
        icon = file('src/main/resources/icon.png')

        installerOptions = ['--linux-menu-group','Utilities', '--linux-app-category', 'text',
                            '--linux-shortcut',
                            '--resource-dir', 'linuxOverride',
                            '--linux-deb-maintainer', 'zach@keendev.app',
                            '--description', 'an encrypted notepad and journal.']

        jvmArgs = ['--add-opens', 'java.desktop/sun.awt.X11=cryptopad.main']


    }

    forceMerge('com.formdev.flatlaf', 'com.formdev.flatlaf.intellijthemes', 'org.bouncycastle.provider')
}





dependencies {
    //testing dependencies
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation group:'org.mockito', name: 'mockito-core', version: '5.15.2'
    testImplementation group:'org.mockito', name: 'mockito-junit-jupiter', version: '5.15.2'

    //application dependencies
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.80'
    implementation 'com.formdev:flatlaf:3.4'
    implementation 'com.formdev:flatlaf-intellij-themes:3.5.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'


}

jar {
    manifest {
        attributes 'Main-Class': 'drivers.GuiDriver'
    }



    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/versions/**'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'


}

test {
    useJUnitPlatform()
}
